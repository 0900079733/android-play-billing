apply plugin: 'com.android.feature'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")
// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()
// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    baseFeature = true

    compileSdkVersion Integer.valueOf(project.TARGET_SDK_VERSION)
    buildToolsVersion project.TOOLS_VERSION

    defaultConfig {
        minSdkVersion Integer.valueOf(project.MINIMUM_SDK_VERSION)
        targetSdkVersion project.TARGET_SDK_VERSION
        versionCode Integer.valueOf(FEATURE_VERSION_CODE)
        versionName FEATURE_VERSION_NAME
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        // Google Play Billing APIs only work with apps signed for production.
        debug {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation project(':shared-module')

    application project(':mobile')
    wearApp project(':wear')

    androidTestApi('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testApi 'junit:junit:4.12'
}
