apply plugin: 'com.android.application'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")
// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()
// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion Integer.valueOf(project.TARGET_SDK_VERSION)
    buildToolsVersion project.TOOLS_VERSION
    defaultConfig {
        applicationId project.APP_ID
        // We need to hard-code 17 version here due to the leanback library
        minSdkVersion Integer.valueOf(17)
        targetSdkVersion Integer.valueOf(project.TARGET_SDK_VERSION)
        versionCode Integer.valueOf(project.TV_VERSION_CODE)
        versionName project.TV_VERSION_NAME
    }
    buildTypes {
        // Google Play Billing APIs only work with apps signed for production.
        debug {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation "com.android.support:leanback-v17:$project.SUPPORT_VERSION"
    implementation project(':shared-module')
}
